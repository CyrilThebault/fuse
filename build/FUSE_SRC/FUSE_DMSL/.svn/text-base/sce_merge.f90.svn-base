PROGRAM SCE_MERGE
! ---------------------------------------------------------------------------------------
! Creator:
! Martyn Clark, 2009
! ---------------------------------------------------------------------------------------
! Purpose:
! Driver program to merge SCE runs from multiple models
! ---------------------------------------------------------------------------------------
USE nrtype                                                ! variable types, etc.
USE ddirectory                                            ! directory for data files
! data modules
USE model_defn,nstateFUSE=>nstate                         ! model definition structures
USE multiparam, ONLY: LPARAM, PARATT, NUMPAR              ! parameter metadata structures
USE multistats, ONLY: PCOUNT, MOD_IX                      ! parameter set / model counters
! access to model simulation modules
USE fuse_rmse_module                                      ! run model and compute the root mean squared error
IMPLICIT NONE
! ---------------------------------------------------------------------------------------
! (0) GET COMMAND-LINE ARGUMENTS
! ---------------------------------------------------------------------------------------
CHARACTER(LEN=120)                     :: FILE_LIST       ! list of NetCDF output files for SCE
! ---------------------------------------------------------------------------------------
! (1) PRELIMINARIES... GET DATA AND NUMERIX DECISIONS
! ---------------------------------------------------------------------------------------
! get model forcing data
INTEGER(I4B)                           :: NTIM            ! number of time steps
INTEGER(I4B)                           :: INFERN_START    ! start of inference period
! ---------------------------------------------------------------------------------------
! (2) READ LIST OF OUTPUT FILES, AND RUN MODEL FOR BEST PARAMETER SET IN EACH ONE
! ---------------------------------------------------------------------------------------
INTEGER(I4B)                           :: I,J,K           ! looping   
INTEGER(I4B)                           :: IERR            ! error code for reading input files
LOGICAL(LGT)                           :: LEXIST          ! .TRUE. if the file exists
INTEGER(I4B)                           :: NMODEL          ! number of models in the file list
CHARACTER(LEN=120)                     :: FILE_NAME       ! name of single NetCDF output file
INTEGER(I4B)                           :: ONEMOD=1        ! just one model in output file
LOGICAL(LGT)                           :: OUTPUT_FLAG     ! switch off/on model output
INTEGER(I4B)                           :: MPAR            ! number of model parameters
REAL(SP), DIMENSION(:), ALLOCATABLE    :: XPAR            ! model parameters
REAL(SP)                               :: RMSE            ! root mean squared error
! ---------------------------------------------------------------------------------------
! (0) GET COMMAND-LINE ARGUMENTS
! ---------------------------------------------------------------------------------------
! pad FILE_LIST with blanks
DO I=1,LEN(FILE_LIST); FILE_LIST(I:I)=' '; END DO
! read input filename from the command line
CALL GETARG(1,FILE_LIST)
IF (LEN_TRIM(FILE_LIST).EQ.0) STOP '1st command-line argument is missing (FILE_LIST)'
! ---------------------------------------------------------------------------------------
! (1) PRELIMINARIES... GET DATA AND NUMERIX DECISIONS
! ---------------------------------------------------------------------------------------
! Define  method/parameters used for numerical solution
CALL GETNUMERIX()         
! Read data from the "BATEA-compliant" ASCII files
CALL GETFORCING(INFERN_START,NTIM)
! Read parameter metadata (parameter bounds etc.) for all models
CALL GETPARMETA()        
! ---------------------------------------------------------------------------------------
! (2) READ LIST OF OUTPUT FILES, AND RUN MODEL FOR BEST PARAMETER SET IN EACH ONE
! ---------------------------------------------------------------------------------------
! check that the file containing list of SCE output files exists
INQUIRE(FILE=TRIM(FILE_LIST),EXIST=LEXIST)
IF (.NOT.LEXIST) STOP 'file containing list of SCE output files does not exist'
! get number of output files (models) to process
NMODEL = 0
OPEN(21,FILE=TRIM(FILE_LIST))
 DO; READ(21,*,IOSTAT=IERR) FILE_NAME; IF (IERR.NE.0) EXIT; NMODEL=NMODEL+1; END DO
CLOSE(21)
! Define output file names (shared in MODULE model_defn)
FNAME_NETCDF = TRIM(OUTPUT_PATH)//'SCE_merge.nc'
! Define NetCDF output files (only write parameters and summary statistics)
OUTPUT_FLAG = .TRUE.     ! .TRUE. if desire time series output
CALL DEF_PARAMS(NMODEL)  ! define model parameters (initial CREATE)
CALL DEF_SSTATS()        ! define summary statistics (REDEF)
IF (OUTPUT_FLAG) CALL DEF_OUTPUT(NTIM)    ! define model time series (REDEF)
! initialize the model index (stared in module multistats)
MOD_IX = 0
! loop thtough output files
OPEN(21,FILE=TRIM(FILE_LIST))
 DO   ! loop through output files
  ! get output filename
  READ(21,*,IOSTAT=IERR) FILE_NAME
  IF (IERR.NE.0) EXIT
  ! identify model (populate SMODL)
  CALL GET_SMODEL(FILE_NAME,ONEMOD)
  ! Define list of states and parameters for the current model
  CALL ASSIGN_STT()        ! state definitions are stored in module model_defn
  CALL ASSIGN_FLX()        ! flux definitions are stored in module model_defn
  CALL ASSIGN_PAR()        ! parameter defintions are stored in module multiparam
  ! get final parameter set
  MPAR = NUMPAR            ! (number of model parameter sets)
  ALLOCATE(XPAR(MPAR),STAT=IERR); IF (IERR.NE.0) STOP ' problem allocating XPAR '
  CALL GET_FPARAM(FILE_NAME,ONEMOD,MPAR,XPAR)
  WRITE(*,'(20(A11,1X))') LPARAM(1:NUMPAR)
  WRITE(*,'(20(F11.3,1X))') XPAR(1:NUMPAR)
  ! compute derived model parameters (bucket sizes, etc.)
  CALL PAR_DERIVE()
  ! define indices for data write
  PCOUNT=0          ! ensure the parameter counter is set to zero (incremented in fuse_rmse)
  MOD_IX=MOD_IX + 1 ! increment the model index
  ! run zee model
  CALL FUSE_RMSE(XPAR,RMSE,OUTPUT_FLAG)
  ! deallocate space
  DEALLOCATE(XPAR, STAT=IERR); IF (IERR.NE.0) STOP ' problem deallocating XPAR '
 END DO ! (looping through output files)
CLOSE(21)
STOP
END PROGRAM SCE_MERGE
